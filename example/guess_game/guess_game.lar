import time, net, math/rand;

void log(String msg)
{
    println("[%s] %s".(time.format_time("2006-01-02 15:04:05", time.time()), msg));
}

class Client
{
    net.TcpConn sock;
    char[] result;

    Client(net.TcpConn sock)
    {
        this.sock = sock;
        this.result = new char[4];
        bool[] selected = new bool[10];
        for (int i = 0; i < 4; )
        {
            char c = (char)rand.rand_n(10);
            if (selected[c])
            {
                continue;
            }
            selected[c] = true;
            this.result[i] = '0' + c;
            ++ i;
        }
        this.client_log("start with result [%s]".(new String(this.result)));
    }

    void client_log(String s)
    {
        log("client [%s]: %s".(this.sock.remote_addr(), s));
    }

    bool is_valid_input(char[] s, long sz)
    {
        if (sz != 6)
        {
            return false;
        }
        bool[] selected = new bool[10];
        for (uint i = 0; i < 4; ++ i)
        {
            char c = s[i];
            if (c < '0' || c > '9' || selected[c - '0'])
            {
                return false;
            }
            selected[c - '0'] = true;
        }
        return true;
    }

    void send_str(String s)
    {
        this.sock.write(s.to_char_array());
    }
    void send_line(String s)
    {
        this.send_str(s.concat("\r\n"));
    }

    void _run()
    {
        char[] recv_buf = new char[1000];
        int[] rsp = new int[2];
        while (true)
        {
            this.send_str("Guess a number, 4 different digits:");

            long recved_size = this.sock.read(recv_buf);
            if (recved_size == 0)
            {
                this.client_log("client over");
                return;
            }

            if (recved_size < 2 || recv_buf[recved_size - 1] != '\n' || recv_buf[recved_size - 2] != '\r')
            {
                throw("proto error");
            }
            if (recved_size == 2)
            {
                continue;
            }

            if (!this.is_valid_input(recv_buf, recved_size))
            {
                this.send_line("Invalid input");
                continue;
            }

            rsp[0] = 0;
            rsp[1] = 0;
            for (long i = 0; i < 4; ++ i)
            {
                for (long j = 0; j < 4; ++ j)
                {
                    if (this.result[i] == recv_buf[j])
                    {
                        if (i == j)
                        {
                            ++ rsp[0];
                        }
                        else
                        {
                            ++ rsp[1];
                        }
                    }
                }
            }
            if (rsp[0] == 4)
            {
                this.send_line("You got it");
                this.client_log("player win");
                return;
            }
            this.send_line("%dA%dB".(rsp[0], rsp[1]));
        }
    }

    public void run()
    {
        defer
        {
            var c = catch_base();
            if (c != nil)
            {
                this.client_log("client ends with exception [%s], traceback[%s]".(c.throwed().str(), c.traceback()));
            }
        }
        defer this.sock.close();

        this._run();
    }
}

public void main()
{
    net.TcpListener listener = new net.TcpListener(":9999");
    defer listener.close();

    log("game start!");
    while (true)
    {
        defer
        {
            var c = catch_base();
            if (c != nil)
            {
                log("accept new client failed [%s][%s]".(c.throwed().str(), c.traceback()));
            }
        }

        net.TcpConn new_client = listener.accept();
        log("accept success, new client [%s]".(new_client.remote_addr()));

        start_fiber(new Client(new_client));
    }
}
